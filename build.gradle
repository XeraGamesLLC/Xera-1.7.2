plugins {
    id 'java'
}

version = '1.0.0'
group = 'dev.xera'
archivesBaseName = 'XeraClient'

sourceCompatibility = targetCompatibility = '8'
compileJava {
    sourceCompatibility = targetCompatibility = '8'
    options.encoding 'UTF-8'
    options.release = 8  // Ensures Java 8 API compatibility when compiling with newer JDKs
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    embed
    implementation.extendsFrom embed
}

dependencies {
    // i love bush's libraries
    embed 'com.github.therealbush:eventbus:1.0.2'

    // noinspection GradlePackageUpdate
    embed 'com.google.code.gson:gson:2.8.6'

    // add all jars in /libs/
    implementation fileTree('libs')

    embed('club.minnced:java-discord-rpc:2.0.1')
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from(configurations.embed.collect {
        it.isDirectory() ? it : zipTree(it)
    }) {
        exclude 'dummyThing',
                'LICENSE.txt',
                'META-INF/MUMFREY.RSA',
                'META-INF/maven/**',
                'org/**/*.html'
    }

    manifest.attributes(
            'Main-Class': 'Start',
            'Class-Path': '.'
    )
}
